precision mediump float;
varying highp vec2 textureCoordinate;
uniform sampler2D inputImageTexture;

uniform sampler2D SamplerTableTex1;
uniform sampler2D SamplerTableTex2;
uniform sampler2D SamplerTableTex3;

uniform sampler2D SamplerBackTex3;
uniform sampler2D SamplerBackTex1;
uniform sampler2D SamplerBackTex2;

vec3 tableLookUp(vec3 src, sampler2D table)
{
    src.r = texture2D(table, vec2(src.r, 0.0)).r;
    src.g = texture2D(table, vec2(src.g, 0.0)).g;
    src.b = texture2D(table, vec2(src.b, 0.0)).b;
    return src;
}

vec3 screen(vec3 src1, vec3 src2, float alpha)
{
    return mix(src1, src1 + src2 - src1 * src2, alpha);
}

vec3 colorDodgeAdobes(vec3 src1, vec3 src2, vec3 alphas)
{
    return src1 * (1.0 - alphas) + min(src1 + (src1 * src2) / (1.0 - src2), 1.0) * alphas;
}

vec3 colorNegLum(vec3 src)
{
    return src * mat3
    (
     0.915, -0.587, -0.114,
     -0.299, 0.413, -0.114,
     -0.299, -0.587, 0.886
     );
}

vec3 add(vec3 src1, vec3 src2, float alpha)
{
    return mix(src1, src1 + src2, alpha);  //if 'clamp' should be used? that's a question.
}

vec3 overLay(vec3 src1, vec3 src2, float alpha)
{
    if(src1.r < 0.5)
        src2.r = (src1.r * src2.r) * 2.0;
    else src2.r = (src1.r + src2.r) * 2.0 - (src1.r * src2.r) * 2.0 - 1.0;
    
    if(src1.g < 0.5)
        src2.g = (src1.g * src2.g) * 2.0;
    else src2.g = (src1.g + src2.g) * 2.0 - (src1.g * src2.g) * 2.0 - 1.0;
    
    if(src1.b < 0.5)
        src2.b = (src1.b * src2.b) * 2.0;
    else src2.b = (src1.b + src2.b) * 2.0 - (src1.b * src2.b) * 2.0 - 1.0;
    
    return mix(src1, src2, alpha);
}

void main()
{
    vec3 src = texture2D(inputImageTexture, textureCoordinate).rgb;
    vec4 tex1 = texture2D(SamplerBackTex1, textureCoordinate);
    vec4 tex2 = texture2D(SamplerBackTex2, textureCoordinate);
    vec4 tex3 = texture2D(SamplerBackTex3, textureCoordinate);
    
    src = tableLookUp(src, SamplerTableTex1);
    src = screen(src, tex1.rgb, tex1.a);  //Beattle_Src
    
    src = colorDodgeAdobes(src, tableLookUp(src, SamplerTableTex2), tex2.rgb * tex2.a);
    
    src = clamp(src * mat3
                (
                 -0.04, 1.13, 0.09,
                 -0.04, 1.13, 0.09,
                 -0.04, 1.13, 0.09
                 ) - 0.06, 0.0, 1.0);
    vec3 tmp = src;
    
    src = tableLookUp(src, SamplerTableTex3);
    
    src = colorNegLum(src);
    src = add(src, tmp, 1.0);
    src = overLay(src, tex3.rgb, 0.5 * tex3.a);
    gl_FragColor = vec4(src, 1.0);
}
